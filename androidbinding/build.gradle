plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'maven-publish'
}

version = libs.versions.lib.version.get()
group = libs.versions.lib.group.get()

android {
    namespace "jp.programminglife.libpljp.androidbinding"
    compileSdk libs.versions.app.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.app.minSdk.get().toInteger()
        targetSdk libs.versions.app.targetSdk.get().toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
            vendor = JvmVendorSpec.ORACLE
        }
    }

    buildFeatures {
        dataBinding true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = 'libpljp-androidbinding'
                from components.release
            }
        }
    }
}

dependencies {
    api project(':lib')
    api libs.androidx.annotation
    api libs.androidx.legacySupport
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.espresso.core
}
